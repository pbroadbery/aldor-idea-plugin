plugins {
    id 'org.jetbrains.intellij' version "0.2.5"
    id "de.undercouch.download" version "3.2.0"
}


allprojects {
    apply plugin: 'java'
    group = 'org.aldor.idea'
    version = '1.0'

    sourceSets {
        main.java.srcDirs = ['src/main/java', 'src/gen/jflex', 'src/gen/grammar']
        main.resources.srcDir 'resources'

        test.java.srcDir 'test/src/java'
        test.resources.srcDir 'test/src/resources'
    }
    intellij {
        version 'IC-2017.2.6' //IntelliJ IDEA 2016.3 dependency; for releases: https://www.jetbrains.com/intellij-repository/releases
        pluginName 'Aldor Plugin'

        plugins = []

        //updateSinceUntilBuild = false// Was in haskell, but..

        sandboxDirectory = project.rootDir.canonicalPath + "/.sandbox"
    }

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'idea'
apply plugin: 'de.undercouch.download'

import de.undercouch.gradle.tasks.download.Download

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(Test) { systemProperties['aldor.build.skip_ci'] = System.getProperty('aldor.build.skip_ci') }


def libFiles = ["aldor", "algebra", "foam", "foamj", "SpadType"]
dependencies {
    compile project('jps-plugin')
    compile files(
            libFiles.collect({"lib/${it}.jar"}))
}

task copyLibraryFiles(type: Copy) {
    from libFiles.collect({"/home/pab/IdeaProjects/aldor-modules/types/index/jars/${it}.jar"})
    into 'lib'
    eachFile {
        if (it.relativePath.getFile(destinationDir).exists()) {
            it.exclude()
        }
    }
}

task list(dependsOn: configurations.compile) {
    doLast {
        println "classpath = ${configurations.compile.collect { File file -> file.toString()}}"
    }
}

task downloadGrammarKit(type: Download) {
    src 'https://github.com/JetBrains/Grammar-Kit/files/1459993/GrammarKit-2017.1.1.zip'
    dest 'download/GrammarKit.zip'
    overwrite false
    outputs.file file(dest)
}

task downloadJFlexSkel(type: Download) {
    src 'https://raw.githubusercontent.com/JetBrains/intellij-community/master/tools/lexer/idea-flex.skeleton'
    dest 'lib/jflex/idea-flex.skeleton'
    overwrite false
    outputs.file 'lib/jflex/idea-flex.skeleton'
}

task downloadJFlex(type: Download) {
    src 'https://raw.githubusercontent.com/JetBrains/intellij-community/master/tools/lexer/jflex-1.7.0-SNAPSHOT.jar'
    dest 'lib/jflex/jflex-1.7.0-SNAPSHOT.jar'
    overwrite false
}

task cleanLexer {
    doLast {
	for (f in generateLexer.outputs.files) { delete f }
    }
}

task cleanLibs {
    doLast {
        for (f in libFiles) { delete "lib/${f}.jar"}
    }
}

tasks.clean.dependsOn tasks.cleanLexer
tasks.clean.dependsOn tasks.cleanLibs

task unzipGrammar(type: Copy) {
    def zipFile = file('download/GrammarKit.zip')
    def outputDir = 'lib'

    from zipTree(zipFile)
    into outputDir

    inputs.files tasks.downloadGrammarKit.outputs.files
    outputs.file file('lib/GrammarKit/lib/grammar-kit.jar')
}

task generateLexer (type: JavaExec) {
    def dstRoot = "$project.projectDir/src/gen/jflex"
    def pkg = 'aldor/lexer'
    def src = "$project.projectDir/src/main/jflex/${pkg}/aldor.flex"
    def dst = "src/gen/jflex/" + pkg

	doFirst {
            delete file(dst)
        }

        main = 'jflex.Main'
        classpath = files('lib/jflex/jflex-1.7.0-SNAPSHOT.jar')

        args = ['--skel', 'lib/jflex/idea-flex.skeleton', '-d', "${dstRoot}/${pkg}" , file(src)]

        inputs.files files(['lib/jflex/jflex-1.7.0-SNAPSHOT.jar', 'lib/jflex/idea-flex.skeleton', src])
        outputs.dirs fileTree("${dst}").include('**/*.java')
}

tasks.generateLexer.dependsOn tasks.downloadJFlex
tasks.generateLexer.dependsOn tasks.downloadJFlexSkel

parserTask(project, "Aldor", "aldor/psi")
parserTask(project, "Expression", "aldor/expression")

tasks.compileJava.dependsOn tasks.generateAldorParser
tasks.compileJava.dependsOn tasks.generateExpressionParser
tasks.compileJava.dependsOn tasks.generateLexer
tasks.compileJava.dependsOn tasks.copyLibraryFiles

def parserTask(project, parserName, pkg) {
    def tt = tasks.create("generate${parserName}Parser", JavaExec) {
        def dstRoot = "$project.projectDir/src/gen/grammar"
        def src = "$project.projectDir/src/main/grammar/${parserName}.bnf"
	    def dst = "$dstRoot/$pkg"
        classpath(configurations.compile + files('lib/GrammarKit/lib/grammar-kit-2017.1.1.jar'))

        args = [dstRoot, file(src)]
        main = 'org.intellij.grammar.Main'
        inputs.file src
        outputs.dirs fileTree(dst).include('**/*.java')
    }
    def cleaner = tasks.create("clean${parserName}Parser") {
        outputs.upToDateWhen { false }
        doLast {
            for (f in tt.outputs.getFiles()) { delete f }
	}
    }
    tt.dependsOn tasks.unzipGrammar
    tasks.clean.dependsOn cleaner
    return tt
}

test {
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
}
